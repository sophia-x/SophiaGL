#version 330 core

// Interpolated values from the vertex shaders
in vec2 uv;
in vec3 position_worldspace;
in vec3 normal_cameraspace;
in vec3 eye_direction_cameraspace;
in vec3 light_direction_cameraspace;

// Ouput data
out vec3 color;

// Values that stay constant for the whole mesh.
uniform sampler2D texture_in;
uniform vec3 light_position_worldspace;
uniform vec3 light_color;
uniform float light_power;
uniform vec3 material_ambient_color_ratio;
uniform vec3 material_specular_color;
uniform float material_specular_ratio;

void main(){
	// Material properties
	vec3 material_diffuse_color = texture(texture_in, uv).rgb;
	vec3 material_ambient_color = material_ambient_color_ratio * material_diffuse_color;

	// Distance to the light
	float distance = length(light_position_worldspace - position_worldspace);

	// Normal of the computed fragment, in camera space
	vec3 n = normalize(normal_cameraspace);
	// Direction of the light (from the fragment to the light)
	vec3 l = normalize(light_direction_cameraspace);
	
	// Cosine of the angle between the normal and the light direction, 
	// clamped above 0
	//  - light is at the vertical of the triangle -> 1
	//  - light is perpendicular to the triangle -> 0
	//  - light is behind the triangle -> 0
	float cos_theta = clamp( dot( n,l ), 0,1 );
	
	// Eye vector (towards the camera)
	vec3 E = normalize(eye_direction_cameraspace);
	// Direction in which the triangle reflects the light
	vec3 R = reflect(-l,n);
	// Cosine of the angle between the Eye vector and the Reflect vector,
	// clamped to 0
	//  - Looking into the reflection -> 1
	//  - Looking elsewhere -> < 1
	float cos_alpha = clamp( dot( E,R ), 0,1 );
	
	color = 
		// Ambient : simulates indirect lighting
		material_ambient_color +
		// Diffuse : "color" of the object
		material_diffuse_color * light_color * light_power * cos_theta / (distance*distance) +
		// Specular : reflective highlight, like a mirror
		material_specular_color * light_color * light_power * pow(cos_alpha, material_specular_ratio) / (distance*distance);
}